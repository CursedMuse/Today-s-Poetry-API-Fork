apply plugin: 'com.android.library'

android {
	compileSdkVersion 30

	defaultConfig {
		minSdkVersion 18
		targetSdkVersion 30
		buildConfigField "String", "LIB_VERSION_NAME", "\"${VERSION_NAME}\""
	}
}

dependencies {
	compileOnly 'androidx.appcompat:appcompat:1.3.0'
	implementation 'com.google.code.gson:gson:2.8.6'
}

// 发布到本地目录，非必须
ext.localReleaseDest = "${buildDir}/release/${VERSION_NAME}"

task androidJavadocs(type: Javadoc) {
	source = android.sourceSets.main.java.srcDirs
	classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
	classifier = 'javadoc'
	from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
	classifier = 'sources'
	from android.sourceSets.main.java.sourceFiles
}

artifacts {
	archives androidSourcesJar
	archives androidJavadocsJar
}